size 22 43
set font pstr fontlwidth 0.08 hei 1.2 just CC
fontSz=0.8

verSz=5
horSz=19
set lwidth 0.04
set font pstr
set hei .6
set hei 0.7
MarkSz = 0.1
curveW=0.05

scl_th=0.5
scl_ph=0.05

labl$='../19A-1/files/DLtst'
if(nargs() > 0 ) then 
    labl$=arg$(1)
end if
DataFile$=labl$+"IntfSpecPolrz_d.dat"    ! DLsIntfSpecPolrz_d.dat 
   fopen DataFile$ inchanw read
   freadln inchanw tmin tmax   TimeBase CenLoc1 CenLoc2 CenLoc3 Name$ PixPowOpt zero N_smth
SpecDataFile$=labl$+"Trace_01.dat"    ! 
   fopen SpecDataFile$ inchanS read
   freadln inchanS s_min   s_max  ! sample numbers

N_slice = (s_max-s_min)/N_smth
s_Med = (s_max+s_min)/2
K=1
Markers = N_slice
while Markers > 5
   K = K*2
   Markers = N_slice/K
next
K = K/2

set just TL
amove 1.0  0.9
write "tmin=" tmin "(" CenLoc1/1000. "," CenLoc2/1000. "," CenLoc3/1000. ")"
amove 15.5  0.9
text slice time [samples]
!write "k,Markers,N_smth: " K Markers N_smth  
!amove 10.  11
!write "s_min   s_max: "   s_min   s_max
!amove 10.  12
!write "s_Med N_slice: "   s_Med N_slice
amove 2.  2
begin graph
  size horSz verSz
  vscale 1
  hscale 1
   xaxis min s_min max s_max hei fontSz
   yaxis  min 0. 
   y1axis off
   y2axis on
     data SpecDataFile$ d1=c1,c4 d2=c1,c5 ignore 1
     d1 line lwidth curveW color magenta
     d2 line lwidth curveW color green
end graph
set lwidth 0.01
i=0
   amove xg(s_Med+i*K*N_smth) 2
   aline xg(s_Med+i*K*N_smth) 2+8*verSz
while i < Markers-1
   i = i + 1
   amove xg(s_Med+i*K*N_smth) 2
   aline xg(s_Med+i*K*N_smth) 2+8*verSz
   amove xg(s_Med-i*K*N_smth) 2
   aline xg(s_Med-i*K*N_smth) 2+8*verSz
   !amove 10.  15+i
   !write "i: " i
Next
set lwidth 0.04
amove 2.  2
begin graph
   size horSz verSz
   vscale 1
   hscale 1
   xaxis min s_Min max s_Max hei fontSz ! dticks  1 dsubticks .2  ! length=7
   !xaxis hei fontSz ! dticks  1 dsubticks .2  ! length=7
   yaxis min 0. ! max +100 ! log ! min scl_min max scl log
   y2axis off
   !y1axis on
   !data DataFile$ d1=c2,c3   ignore 1 !
   data DataFile$ d1=c1,c3   ignore 1 !
   d1 marker circle  msize MarkSz  lstyle 2 ! lwidth 0.05
end graph
begin key
   position tr
   line lwidth curveW color magenta text \phi
   line lwidth curveW color green text \theta
end key
set just TL
amove xg(s_Min)+0.5 verSz+2.-0.5
write 'I'
!set hei .7
!amove 2+0.5*HorSz  0.7


amove 2.  2.+verSz
begin graph
   size horSz verSz
   vscale 1
   hscale 1
   xaxis min s_Min max s_Max hei fontSz ! dticks  1 dsubticks .2  ! length=7
   xlabels off
   yaxis min -105 max +105 nofirst
   let d7=-100
   d7 line lstyle 1 lwidth 0.01 color red
   let d8=0
   d8 line lstyle 1 lwidth 0.01 color green
   let d9=100
   d9 line lstyle 1 lwidth 0.01 color blue
   data DataFile$ d1=c1,c7 d2=c1,c8  ignore 1
   d1 marker circle msize MarkSz  err d2 errwidth .1 lstyle 2
end graph
set just TR
amove xg(s_Max)-0.3 yg(100)-0.3
write 'Q/I [%]'

amove 2.  2.+2*verSz
begin graph
   size horSz verSz
   vscale 1
   hscale 1
   xaxis min s_Min max s_Max hei fontSz ! dticks  1 dsubticks .2  ! length=7
   xlabels off
   yaxis min -105 max +105 nofirst
   let d8=0
   d8 line lstyle 1 lwidth 0.01 color green
   data DataFile$ d1=c1,c9 d2=c1,c10  ignore 1
   d1 marker circle msize MarkSz  err d2 errwidth .1 lstyle 2
end graph
set just TR
amove xg(s_Max)-0.3 yg(100)-0.3
write 'U/I [%]'

amove 2.  2.+3*verSz
begin graph
   size horSz verSz
   vscale 1
   hscale 1
   xaxis min s_Min max s_Max hei fontSz ! dticks  1 dsubticks .2  ! length=7
   xlabels off
   yaxis min -105 max +105 nofirst
   let d8=0
   d8 line lstyle 1 lwidth 0.01 color green
   data DataFile$ d1=c1,c11 d2=c1,c12  ignore 1
   d1 marker circle  msize MarkSz   err d2 errwidth .1 lstyle 2
end graph
set just TR
amove xg(s_Max)-0.3 yg(100)-0.3
write 'V/I [%]'

amove 2.  2.+4*verSz
begin graph
   size horSz verSz
   vscale 1
   hscale 1
   xaxis min s_Min max s_Max hei fontSz ! dticks  1 dsubticks .2  ! length=7
   xlabels off
   yaxis min 0.0 max +100 nofirst
   data DataFile$ d1=c1,c13 d2=c1,c14  ignore 1
   d1 marker circle  msize MarkSz   err d2 errwidth .1 lstyle 2
end graph
set just TR
amove xg(s_Max)-0.3 yg(100)-0.3
write 'I3/I [%]'

amove 2.  2.+5*verSz
begin graph
   size horSz verSz
   vscale 1
   hscale 1
   xaxis min s_Min max s_Max hei fontSz ! dticks  1 dsubticks .2  ! length=7
   xlabels off
   yaxis min 0.0 
   data DataFile$ d1=c1,c23  ignore 1
   d1 marker circle  msize MarkSz   lstyle 2
end graph
set just TR
amove xg(s_Max)-0.3 2.+6*verSz-0.3
write 'Chi^2/DoF'

amove 2.  2.+6*verSz
begin graph
   size horSz verSz
   vscale 1
   hscale 1
   xaxis min s_Min max s_Max hei fontSz ! dticks  1 dsubticks .2  ! length=7
   xlabels off
   yaxis min 0.0 
   data DataFile$ d1=c1,c24  d2=c1,c25  d3=c1,c26  ignore 1
   d1 marker fsquare  msize MarkSz  color green lstyle 2
   d2 marker cross  msize MarkSz  color red lstyle 2
   d3 marker fcircle  msize MarkSz  color blue lstyle 2
end graph
begin key
   position tl
   marker fsquare  msize MarkSz  color green text "UnPol"
   marker cross  msize MarkSz  color red text "LinPol"
   marker fcircle  msize MarkSz  color blue text "CirPol"
end key

amove 2.  2.+7*verSz
begin graph
   size horSz verSz
  vscale 1
  hscale 1
   xaxis min s_min max s_max
   x1labels off
   x2labels on
   yaxis  
   !y1axis off
   !y2axis on
     data SpecDataFile$ d1=c1,c2 d2=c1,c3 ignore 1
     d1 line lwidth curveW color red
     d2 line lwidth curveW color blue
end graph
begin key
   position tl
   line lwidth curveW color red text "even ref ant"
   line lwidth curveW color blue text "odd ref ant"
end key


