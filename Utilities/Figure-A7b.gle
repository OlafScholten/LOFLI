!size   23 38
Z1=5
Z=Z1
! Z=0  ! if active, omit side panels
fontSz=1.2
!fontSz=1.0
!fontSz=0.8
bsh=2.5*fontSz    ! 1.5
HorOff=1.5+bsh
VerOff=2.0+bsh
B=14+Z1-Z
size   HorOff+B+Z+bsh VerOff+B+Z+2*Z1+3*bsh ! ----------------------------
!
file$='files/20A8o18SE-all'
if(nargs() > 0 ) then 
    file$=arg$(1)  ! Should include the appropriate path to make this script machine independent
end if
filedat$=file$+'.dat'
!filedat$='../files/'+file$+'.dat'
fopen filedat$ inchan read
freadln inchan xMin xMax yMin yMax zMin zMax tMin tMax ZmBx$ t_offst  ! there needs to be a trailing space
freadln inchan NTracks EventNr Q t_start label$ AmplitudePlot a1 b1 c1 a2 b2 c2 d2
!NTracks=0   !  omit plotting leader tracks
if(ZmBx$ = 'NoBox') then
    bbx=-1
else
    fopen 'files/....'+'.dat' inbox read
    freadln inbox bxMin bxMax byMin byMax bzMin bzMax btMin btMax
    bbx=+1
end if 

H=B*(yMax-yMin)/(xMax-xMin)
set lwidth 0.04
set font pstr
set hei fontSz
MinMSze=0.3
if(EventNr > 900) then
   MinMSze=MinMSze*30./sqrt(EventNr)
end if
MSze= .04/(xMax-xMin)  ! should be about 1 m on the scale of the plot
If (MSze < MinMSze) then MSze=MinMSze
!amove 6 5
!write ZmBx$ t_offst EventNr NTracks
!NTracks=0

declare sub Clr Cscale
include "RdYlBl.gle"
include "ClrOld.gle"
include "MaxMin.gle"
include "ColorLegenda.gle"

sub SetDiscrColor i
    if i = 1 then
        set color lightskyblue ! blue
    else if i = 2 then
        set color plum ! magenta
    else if i = 3 then
        set color salmon ! red
    else if i = 4 then
        set color peru ! brown
    else if i = 5 then
        set color lightgreen ! green
    else 
      set color grey10
    end if
end sub


sub Clr Cscale
  !RdYlBl(Cscale)
  ClrOld(Cscale)
end sub



If AmplitudePlot <= 0 then  ! logical operation apparently does not work around subroutine definition
   top=VerOff+h+Z+bsh+Z1+fontSz
   AmplitudePlot=0
Else
   top=VerOff+h+Z+2*bsh+2.*Z1+fontSz
End If
   FactAmp=AmplitudePlot/4.  ! to keep the size of the dot for amplitude=10 independent of AmplitudePlot
   ConstAmp=1.-AmplitudePlot


!   sub 2Dtime dt$ ds$ dmin dmax dI$
!       local DDx = dmax-dmin
!       for i = 1 to ndata(dt$)
!           local x = dataxvalue(ds$,i)
!           local y = datayvalue(ds$,i)
!           local Cscale = (dataxvalue(dt$,i)-dmin)/DDx
!           amove xg(x) yg(y)
!           Clr(Cscale)
!           marker fcircle MSze
!       next i
!       set color black
!   end sub
   sub 2Dtime dt$ ds$ dmin dmax dI$
       local DDx = dmax-dmin
       for i = 1 to ndata(dt$)
           local x = dataxvalue(ds$,i)
           local y = datayvalue(ds$,i)
           If (AmplitudePlot=0) then
              MSzeI=MSze
           Else
              !Local Int = log10(datayvalue(dI$,i))
              !If(Int<0.2) then
              ! Int=0.2
              !End If
              !MSzeI=MSze*(1.+log10(datayvalue(dI$,i)*FactAmp+ConstAmp))
              MSzeI=MSze*sqrt(datayvalue(dI$,i)*FactAmp+ConstAmp)
              !MSzeI=MSze*(datayvalue(dI$,i)*FactAmp+ConstAmp)
           End If
           local Cscale = (dataxvalue(dt$,i)-dmin)/DDx
           amove xg(x) yg(y)
           Clr(Cscale)
           !marker circle MSzeI  ! 
           marker fcircle MSzeI
       next i
       set color black
   end sub

! Start plotting
amove HorOff top 
write label$  "; Q="  Q "ns, "  EventNr " sources"

amove HorOff VerOff+h+Z+bsh ! (time,h) ---------------------------------------------------
begin graph
   size B+Z Z1
   vscale 1
   hscale 1
   xaxis min tMin max tMax hei fontSz ! dticks  1 dsubticks .2  ! length=7
   yaxis min zMin max zMax hei fontSz dticks 2 ! 1 dsubticks .2        ! length=9
   data filedat$ d3=c2,c1 d4=c2,c5  d5=c2,c6    ignore 2
   ! d4 marker cross msize 0.2 color blue
end graph
if(bbx>0) then
   amove xg(btMin) yg(bzMin)
   box (xg(btMax)-xg(btMin)) (yg(bzMax)-yg(bzMin)) 
end if
! amove 4 9
! write NTracks
for i =1 to NTracks
   istr$ = format$(i, "fix 0")
   !  istr$ = format$(i, "prefix 2 fix 0")
   fileldr$=file$+istr$+'.dat'       ! obtain this leader structure
   ! amove 3 10+i
   ! write fileldr$
   fopen fileldr$ leadr read
   set lwidth 0.2
   SetDiscrColor(i)
   freadln leadr tL xL yL zL
   amove xg(tL) yg(zL)
   until feof(leadr)
      freadln leadr tL xL yL zL
      aline xg(tL) yg(zL) 
   next
   set color black
   set lwidth 0.04
   fclose leadr
next i
dmax = dmaxx(d3)
dmin = dminx(d3)
2Dtime(d3,d4,dmin,dmax,d5)

if(Z>0) then   
   amove HorOff VerOff+h  ! (E-W,h) -------------------------------
   begin graph
      size B Z
      vscale 1
      hscale 1
      xaxis min xMin max xMax ! hei fontSz nofirst nolast ! dticks  1 dsubticks .2 hei fontSz nofirst ! length=7
      yaxis min zMin max zMax hei fontSz nofirst dticks 2 ! 1 dsubticks .2 hei fontSz       ! length=9
      xlabels off
      data filedat$ d3=c2,c1 d4=c3,c5  d5=c2,c6    ignore 2
      ! d4 marker cross msize 0.1 color blue
   end graph
   if(bbx>0) then
      amove xg(bxMin) yg(bzMin)
      box (xg(bxMax)-xg(bxMin)) (yg(bzMax)-yg(bzMin)) 
   end if
   for i =1 to NTracks
      istr$ = format$(i, "fix 0")
      fileldr$=file$+istr$+'.dat'       ! obtain this leader structure
      fopen fileldr$ leadr read
      set lwidth 0.2
      SetDiscrColor(i)
      freadln leadr tL xL yL zL
      amove xg(xL) yg(zL)
      until feof(leadr)
         freadln leadr tL xL yL zL
         aline xg(xL) yg(zL) 
      next
      set color black
      set lwidth 0.04
      fclose leadr
   next i
   dmax = dmaxx(d3)
   dmin = dminx(d3)
   2Dtime(d3,d4,dmin,dmax,d5)
    
   amove HorOff+b VerOff   ! (h,N-S) -----------------------------
   begin graph
      size Z H
      vscale 1
      hscale 1
      xaxis min zMin max zMax hei fontSz nofirst  dticks 2 ! 1 dsubticks .2  ! length=7
      yaxis min yMin max yMax hei fontSz  nolast !  dticks  1 dsubticks .2      ! length=9
      ylabels off
      data filedat$ d3=c2,c1 d4=c5,c4  d5=c2,c6    ignore 2
      ! d4 marker cross msize 0.3 color blue
   end graph
   if(bbx>0) then
      amove xg(bzmin) yg(bymin)
      box (xg(bzMax)-xg(bzMin)) (yg(byMax)-yg(byMin)) 
   end if
   for i =1 to NTracks
      istr$ = format$(i, "fix 0")
      fileldr$=file$+istr$+'.dat'       ! obtain this leader structure
      fopen fileldr$ leadr read
      set lwidth 0.2
      SetDiscrColor(i)
      freadln leadr tL xL yL zL
      amove xg(zL) yg(yL)
      until feof(leadr)
         freadln leadr tL xL yL zL
         aline xg(zL) yg(yL) 
      next
      set color black
      set lwidth 0.04
      fclose leadr
   next i
   dmax = dmaxx(d3)
   dmin = dminx(d3)
   2Dtime(d3,d4,dmin,dmax,d5)
end if

amove HorOff VerOff     ! (E-W,N-S) ---------------------------
begin graph
   size B H
   vscale 1
   hscale 1
   xaxis min xMin max xMax hei fontSz ! nolast ! ticks 1 dsubticks .2 hei fontSz !nolast ! length=7
   yaxis min yMin max yMax hei fontSz ! nolast !  dticks 1 dsubticks .2 hei fontSz  nolast     ! length=9
   !  yaxis  min 0 max 2e-8  hei fontSz ! dticks 5 dsubticks 1
   axis grid
   ticks color grey40  lstyle 2
   xticks length .3
   yticks length .3
   xsubticks length .1
   ysubticks length .1
   data filedat$ d3=c2,c1 d4=c3,c4  d5=c2,c6 ignore 2
   ! d4 marker cross msize 0.2 color blue
   !data file$+'1.dat' d5=c2,c3
   !d5 smoothm  line lwidth 0.25 color green
end graph
if(bbx>0) then
    amove xg(bxmin) yg(bymin)
    box (xg(bxMax)-xg(bxMin)) (yg(byMax)-yg(byMin)) 
end if
for i =1 to NTracks
  istr$ = format$(i, "fix 0")
  fileldr$=file$+istr$+'.dat'       ! obtain this leader structure
  fopen fileldr$ leadr read
  set lwidth 0.2
  SetDiscrColor(i)
  freadln leadr tL xL yL zL
  amove xg(xL) yg(yL)
  until feof(leadr)
    freadln leadr tL xL yL zL
    aline xg(xL) yg(yL) 
  next
  set color black
  set lwidth 0.04
  fclose leadr
next i
dmax = dmaxx(d3)
dmin = dminx(d3)
2Dtime(d3,d4,dmin,dmax,d5)
   
if(Z>0) then   ! plot colorpalette
    x0 = HorOff+0.2+B+fontSz/2.
    y0 = VerOff+0.7+h
    !x0 = 4.+B
    !y0 = 5.+h
    Leng = Z-1
    ClrPltt(x0,y0,Leng,Dmin,Dmax)
end if

If (AmplitudePlot>0) then
amove HorOff VerOff+h+Z+bsh+Z1+2.*fontSz     ! (Amplitude) ---------------------------
   begin graph
     size B+Z z1
     vscale 1
     hscale 1
     xaxis log min 2. ! max xMax hei fontSz ! nolast ! ticks 1 dsubticks .2 hei fontSz !nolast ! length=7
     yaxis log min 0.5 hei fontSz ! nolast !  dticks 1 dsubticks .2 hei fontSz  nolast     ! length=9
     data filedat$   d5=c2,c6 ignore 2
     let d6 = hist d5 step 1 !!!dm [from x1] [to x2] [bins n] [step n]
     d6 line hist
     let d7 = b1*exp(-a1*x-c1/(x*x)) step 40 from 2 !! with a LOG xaxis scale STEP is taken as the number of steps n
     d7 line color red
     !let d8 = b2*exp(-a2*x-c2/(x*sqrt(x))) step 40 from 2 !! with a LOG xaxis scale STEP is taken as the number of steps n
     let d8 = b2*(x/10.)^(-a2)*exp(-c2*10./x-d2*100./x^2) step 40 from 2 !! with a LOG xaxis scale STEP is taken as the number of steps n  ! exp(-c2*100./x^2))
     d8 line color blue
   end graph
   set just TR 
   rmove B+Z-fontSz/2 Z1-fontSz/2
   text Amplitude distribution
End If

!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!!
set hei fontSz
set just CC 
amove HorOff+0.5*b  VerOff-1.7*fontSz
text Easting [km]
if(Z>0) then   
   amove HorOff+0.5*z+B  VerOff-1.7*fontSz
    text Height [km]
end if
amove HorOff+0.5*(B+z)  VerOff+h+z+bsh-1.7*fontSz
!text time [ms] (t_offst
write "time [ms] (+" t_offst " [ms])"

amove 1.5 VerOff+0.5*h
begin rotate 90
   text Northing [km]
   rmove 0.5*h+Z/2 0 
   if(Z>0) then   
       text Height [km]
   end if
   rmove Z/2+bsh+Z1/2 0
   text Height [km]
end rotate

translate -0. -0.5
! include feyn.gle
include note.gle
@note "EventsMap"+"-"+file$
translate 0 2
!end rotate
