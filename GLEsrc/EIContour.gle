! contour
size 24 20
dir$=''
if(nargs() > 0 ) then 
    dir$=arg$(1)
end if
set font texcmr
set lwidth .03
include "color.gle"
!dfile$="Intf"
B1=5.
H1=5.

sub contour_labels file$ fmt$
   ! draws the labels on a contour plot
   ! file$ = filename of file with label data
   ! fmt$ = format string used to format labels
   set just cc hei 0.25 color blue
   fopen file$+"-clabels.dat" f1 read
   until feof(f1)
      fread f1 x y i v
      amove xg(x) yg(y)
      s$ = format$(v, fmt$)
      begin box add 0.05 fill white
         write s$
      end box
   next
   fclose f1
end sub
   
sub HeightPanel file$ Titl$
!  interference contour plot
   ! creates the z-value file to be used by the contour command
   begin fitz        ! Produces file  "file.z"  from "file.csv"
      data file$+".csv"
      x from NxL to NxU step 1.
      y from NyL to NyU step 1.
      ncontour 6
   end fitz
   cc_min=0
   cc_Stp=   cc_max/10
   !
   begin contour     ! needed for making contours, produes "file-cdata.dat" and "file-clabels.dat" from "file.z" created earlier
      data file$+".z"
      values from cc_min to cc_max step cc_Stp
      !values 0.5 1 1.5 2 3
   end contour
   !
   begin graph
      size B1 H1
      vscale 1
      hscale 1
      !title "for h="+format$(hght,"fix 1")+" [km]" hei 0.35
      title "power"+"/"+format$(WinLength,"fix 0")+" "+Titl$
      xtitle xLab$
      ytitle yLab$
      xticks color white
      yticks color white
      colormap file$+".z" 500 500 color zmin cc_min zmax cc_max
      !data file$+"-cdata.dat"  ! for the actual drawing of the contours (made by Contour)
      d1 line color white lwidth 0.02
   end graph
   !
   if Titl$="@h=0" then
      amove xg(xgmax)+0.5 yg(ygmin)
      color_range_vertical zmin cc_min zmax cc_max zstep cc_Stp pixels 1500 format "eng 2 append GB"
      !contour_labels file$  "fix 0"
   end if
end sub


Sub EIPlots 
   gsave
   dfile$=dir$+"_EISpeceo.csv"
   fopen dfile$ inchan read
   freadln inchan StartTime_ms CenLoc1 CenLoc2 CenLoc3 DistMax SSm ipol tshft_mus
   dwfile$=dir$+"_EISpec.csv"
   fopen dwfile$ inchanw read
   freadln inchanw cc_max 
   freadln inchanw cc_max N_N N_Elow N_Eup N_hlow N_hup OffSet WinLength d_N d_E d_h

   amove 2 15
   begin graph
      size 3*B1-2 H1-1
      vscale 1
      hscale 1
      xaxis min 0 max 328
      yaxis log min 1.e-2
      title "power @( "+format$(CenLoc1,"fix 3")+", "+format$(CenLoc2,"fix 3")+", "+format$(CenLoc3,"fix 3")+")km"
      xtitle "t [\mu s] +"+format$(StartTime_ms,"fix 3")+" [ms] - "+format$(tshft_mus,"fix 3")+" [ms]"
      x1ticks length -.2
      x2ticks length +.2
      ytitle "power"
      data dfile$ d1=c1,c2 d2=c1,c3   ignore 1
      d1 line color green lwidth 0.02
      d2 line color red lwidth 0.02
   end graph
   begin key
       nobox
       position tr
       set color black
       line color green lwidth 0.02 text "Y-ref, even"
       line color red lwidth 0.02 text "X-ref, odd"
   end key
   amove xg(offset/200.) yg(0.7e-2) 
   marker ftriangle 0.5
   aline xg(offset/200.) yg(10)
   amove xg((offset+WinLength)/200.) yg(0.7e-2) 
   marker ftriangle 0.5
   aline xg((offset+WinLength)/200.) yg(10)
   
   OS$ = format$(OffSet,"fix 0")
   amove 10 8
   begin graph
      size B1 H1
      vscale 1
      hscale 1
      xaxis nticks 4
      !yaxis min Ymin max Ymax
      yaxis log min 3.e-3
      title "power spectrum [arb]"
      xtitle "t [samples]+"+OS$
      x1ticks length -.2
      x2ticks length +.2
      !ytitle "power"
      data dwfile$ d1=c1,c2  ignore 2
      d1 line color red lwidth 0.02
      !d2 line color green lwidth 0.02
   end graph
   begin key
       nobox
       position tr
       set color black
       line color red lwidth 0.02 text "(1+2)"
       !line color green lwidth 0.02 text "(1+3)-(2)"
   end key
                                                                                    
   amove 18 8
   begin graph
      size B1 H1
      vscale 1
      hscale 1
      xaxis nticks 4
      !yaxis min Ymin max Ymax
      yaxis !log !min 3.e-3
      title "power spectrum [Gal.bckgr]"
      xtitle "t [samples]+"+OS$
      x1ticks length -.2
      x2ticks length +.2
      !ytitle "power"
      data dwfile$ d1=c1,c3 d2=c1,c4 d3=c1,c5 ignore 2
      d1 line color red lwidth 0.02
      d2 line color green lwidth 0.02
      d3 line color blue lwidth 0.02
   end graph
   begin key
       nobox
       position tr
       set color black
       line color red lwidth 0.02 text "Q"
       line color green lwidth 0.02 text "U"
       line color blue lwidth 0.02 text "V"
   end key
                                                                                    
   if ipol=1 then
      EastLab$="d\theta= "+format$(d_E,"fix 3")+"^o" 
      NorthLab$="d\phi= "+format$(d_N,"fix 3")+"^o"
      hLab$="dR= "+format$(d_h,"fix 1")+" [m]"
   else
      NorthLab$="dNorth= "+format$(d_N*pi/180.,"fix 1")+" [m]"
      EastLab$="dEast= "+format$(d_E*pi/180.,"fix 1")+" [m]"
      hLab$="dh= "+format$(d_h,"fix 1")+" [m]"
   end if
   
   amove 2 8
   NxL=-N_N
   NxU=N_N
   NyL=N_Elow
   NyU=N_Eup
   xLab$=NorthLab$
   yLab$=EastLab$
   HeightPanel dir$+"Interferometerd_NE" "@h=0"
   !
   amove 2.5 1
   NyL=N_hlow
   NyU=N_hup
   yLab$=hLab$
   HeightPanel dir$+"Interferometerd_Nh" "@E=0"
   amove 9.5 1
   NxL=N_Elow
   NxU=N_Eup
   xLab$=EastLab$
   HeightPanel dir$+"Interferometerd_Eh" "@N=0"
   amove 5.5 7
   set hei .6
      write "pairs, range="+format$(DistMax,"fix 1")+" [km]"
   grestore
end sub
!=========================================================================


EIPlots 
!translate 15 0
!IntferPlots "1"





